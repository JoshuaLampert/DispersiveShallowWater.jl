@doc raw"""
    BBMBBMVariableEquations1D(gravity, eta0 = 0.0)

BBM-BBM (Benjamin–Bona–Mahony) system in one spatial dimension with spatially varying bathymetry. The equations are given by
```math
\begin{aligned}
  \frac{\partial\eta}{\partial t} + \frac{\partial}{\partial x}((\eta + D)v) - \frac{1}{6}\frac{\partial}{\partial x}(D^2\frac{\partial}{\partial t}\frac{\partial}{\partial x}\eta) &= 0,\\
  \frac{\partial v}{\partial t} + g\frac{\partial\eta}{\partial x} + \frac{\partial}{\partial x}\left(\frac{1}{2}v^2\right) - \frac{1}{6}\frac{\partial^2}{\partial x^2}(D^2\frac{\partial}{\partial t}v) &= 0.
\end{aligned}
```
The unknown quantities of the BBM-BBM equations are the total water height ``\eta`` and the velocity ``v``.
The gravitational constant is denoted by `g` and the bottom topography (bathymetry) ``b = -D > 0``. The water height above the bathymetry is therefore given by
``h = \eta + D``.

One reference for the BBM-BBM system with spatially varying bathymetry can be found in
- Samer Israwi, Henrik Kalisch, Theodoros Katsaounis, Dimitrios Mitsotakis (2022)
  A regularized shallow-water waves system with slip-wall boundary conditions in a basin: theory and numerical analysis
  [DOI: 10.1088/1361-6544/ac3c29](https://doi.org/10.1088/1361-6544/ac3c29)

"""
struct BBMBBMVariableEquations1D{RealT <: Real} <: AbstractBBMBBMEquations{1, 3}
    gravity::RealT # gravitational constant
    eta0::RealT    # constant "lake-at-rest" total water height
end

function BBMBBMVariableEquations1D(; gravity_constant, eta0 = 0.0)
    BBMBBMVariableEquations1D(gravity_constant, eta0)
end

varnames(::BBMBBMVariableEquations1D) = ("eta", "v", "D")

# TODO: Initial condition should not get a `mesh`
"""
    initial_condition_convergence_test(x, t, equations::BBMBBMVariableEquations1D, mesh)

A travelling-wave solution used for convergence tests in a periodic domain.
The bathymetry is constant.

For details see Example 5 in Section 3 from (here adapted for dimensional equations):
- Min Chen (1997)
  Exact Traveling-Wave Solutions to Bidirectional Wave Equations
  [DOI: 10.1023/A:1026667903256](https://doi.org/10.1023/A:1026667903256)
"""
function initial_condition_convergence_test(x,
                                            t,
                                            equations::BBMBBMVariableEquations1D,
                                            mesh)
    g = equations.gravity
    D = 2.0 # constant bathymetry in this case
    c = 5 / 2 * sqrt(D * g)
    rho = 18 / 5
    x_t = mod(x - c * t - xmin(mesh), xmax(mesh) - xmin(mesh)) + xmin(mesh)

    theta = 0.5 * sqrt(rho) * x_t / D
    eta = -D + c^2 * rho^2 / (81 * g) +
          5 * c^2 * rho^2 / (108 * g) * (2 * sech(theta)^2 - 3 * sech(theta)^4)
    v = c * (1 - 5 * rho / 18) + 5 * c * rho / 6 * sech(theta)^2
    return SVector(eta, v, D)
end

# TODO: Initial condition should not get a `mesh`
"""
    initial_condition_sin_bathymetry(x, t, equations::BBMBBMVariableEquations1D, mesh)

An initial condition with a Gaussian bump as initial water height with still water and
a sine-shaped bathymetry.
"""
function initial_condition_sin_bathymetry(x, t, equations::BBMBBMVariableEquations1D, mesh)
    eta = 2.0 + 2.0 * exp(-12.0 * x^2)
    v = 0.0
    D = -1.0 + 0.1 * sinpi(2.0 * x)
    return SVector(eta, v, D)
end

# TODO: Initial condition should not get a `mesh`
"""
    initial_condition_dingemans(x, t, equations::BBMBBMVariableEquations1D, mesh)

The initial condition that uses the dispersion relation of the Euler equations
to approximate waves generated by a wave maker as it is done by experiments of
Dingemans. The topography is a trapesoidal.

References:
- Magnus Svärd, Henrik Kalisch (2023)
  A novel energy-bounded Boussinesq model and a well-balanced and stable numerical discretization
  [arXiv: 2302.09924](https://arxiv.org/abs/2302.09924)
- Maarten W. Dingemans (1994)
  Comparison of computations with Boussinesq-like models and laboratory measurements
  [link](https://repository.tudelft.nl/islandora/object/uuid:c2091d53-f455-48af-a84b-ac86680455e9/datastream/OBJ/download)
"""
function initial_condition_dingemans(x, t, equations::BBMBBMVariableEquations1D, mesh)
    h0 = 0.8
    A = 0.02
    #     omega = 2*pi/(2.02*sqrt(2))
    K = 0.8406220896381442 # precomputed result of find_zero(K -> omega^2 - equations.gravity * K * tanh(K * h0), 1.0) using Roots.jl
    if x < -30.5 * pi / K || x > -8.5 * pi / K
        h = 0.0
    else
        h = A * cos(K * x)
    end
    v = sqrt(equations.gravity / K * tanh(K) * h0) * h / h0
    if x < 11.01 || x >= 33.07
        b = 0.0
    elseif 11.01 <= x && x < 23.04
        b = 0.6 * (x - 11.01) / (23.04 - 11.01)
    elseif 23.04 <= x && x < 27.04
        b = 0.6
    elseif 27.04 <= x && x < 33.07
        b = 0.6 * (33.07 - x) / (33.07 - 27.04)
    else
        error("should not happen")
    end
    eta = h + h0
    D = -b
    return SVector(eta, v, D)
end

function create_cache(mesh,
                      equations::BBMBBMVariableEquations1D,
                      solver::Solver,
                      initial_condition,
                      RealT,
                      uEltype)
    #  Assume D is independent of time and compute D evaluated at mesh points once.
    D = Array{RealT}(undef, nnodes(mesh))
    x = grid(solver)
    for i in eachnode(solver)
        D[i] = initial_condition(x[i], 0.0, equations, mesh)[3]
    end
    K = Diagonal(D .^ 2)
    if solver.D1 isa PeriodicDerivativeOperator
        invImDKD_D = (I - 1 / 6 * sparse(solver.D1) * K * sparse(solver.D1)) \
                     Matrix(solver.D1)
        invImD2K_D = (I - 1 / 6 * sparse(solver.D2) * K) \ Matrix(solver.D1)
    elseif solver.D1 isa PeriodicUpwindOperators
        invImDKD_D = (I - 1 / 6 * sparse(solver.D1.minus) * K * sparse(solver.D1.plus)) \
                     Matrix(solver.D1.minus)
        invImD2K_D = (I - 1 / 6 * sparse(solver.D2) * K) \ Matrix(solver.D1.plus)
    else
        @error "unknown type of first derivative operator"
    end
    tmp1 = Array{RealT}(undef, nnodes(mesh))
    return (invImDKD_D = invImDKD_D, invImD2K_D = invImD2K_D, tmp1 = tmp1)
end

# Discretization that conserves the mass (for eta and v) and the energy for periodic boundary conditions, see
# - Hendrik Ranocha, Dimitrios Mitsotakis and David I. Ketcheson (2020)
#   A Broad Class of Conservative Numerical Methods for Dispersive Wave Equations
#   [DOI: 10.4208/cicp.OA-2020-0119](https://doi.org/10.4208/cicp.OA-2020-0119)
# Here, adapted for spatially varying bathymetry.
function rhs!(du_ode, u_ode, t, mesh, equations::BBMBBMVariableEquations1D,
              initial_condition,
              ::BoundaryConditionPeriodic, solver, cache)
    @unpack invImDKD_D, invImD2K_D, tmp1 = cache

    u = wrap_array(u_ode, mesh, equations, solver)
    du = wrap_array(du_ode, mesh, equations, solver)

    eta = view(u, 1, :)
    v = view(u, 2, :)
    D = view(u, 3, :)
    deta = view(du, 1, :)
    dv = view(du, 2, :)
    dD = view(du, 3, :)
    fill!(dD, zero(eltype(dD)))

    @. tmp1 = -(D * v + eta * v)
    mul!(deta, invImDKD_D, tmp1)

    @. tmp1 = -(equations.gravity * eta + 0.5 * v^2)
    mul!(dv, invImD2K_D, tmp1)

    return nothing
end

@inline function waterheight_total(u, equations::BBMBBMVariableEquations1D)
    return u[1]
end

@inline function velocity(u, equations::BBMBBMVariableEquations1D)
    return u[2]
end

@inline function bathymetry(u, equations::BBMBBMVariableEquations1D)
    return -u[3]
end

@inline function waterheight(u, equations::BBMBBMVariableEquations1D)
    return waterheight_total(u, equations) - bathymetry(u, equations)
end

@inline function energy_total(u, equations::BBMBBMVariableEquations1D)
    eta, v, D = u
    e = equations.gravity * eta^2 + (D + eta) * v^2
    return e
end

@inline entropy(u, equations::BBMBBMVariableEquations1D) = energy_total(u, equations)

# Calculate the error for the "lake-at-rest" test case where eta should
# be a constant value over time
@inline function lake_at_rest_error(u, equations::BBMBBMVariableEquations1D)
    eta, _, _ = u
    return abs(equations.eta0 - eta)
end
